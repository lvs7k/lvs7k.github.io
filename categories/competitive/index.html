<!doctype html>
<html lang="en">
<head>
    <meta charset="utf-8">
    <meta name="viewport" content="width=device-width, initial-scale=1.0">
    <title>Competitive &middot; lvs7k&#39;s blog</title>

    <meta name="description" content="Just a Memo">

    <meta name="generator" content="Hugo 0.49" />
    <meta name="twitter:card" content="summary">
    
    <meta name="twitter:title" content="Competitive &middot; lvs7k&#39;s blog">
    <meta name="twitter:description" content="Just a Memo">

    <meta property="og:type" content="article">
    <meta property="og:title" content="Competitive &middot; lvs7k&#39;s blog">
    <meta property="og:description" content="Just a Memo">

    <link href='//fonts.googleapis.com/css?family=Source+Sans+Pro:400,700|Oxygen:400,700' rel='stylesheet' type='text/css'>
    <link rel="stylesheet" href="//cdnjs.cloudflare.com/ajax/libs/pure/0.6.0/pure-min.css">
    <link rel="stylesheet" href="//cdnjs.cloudflare.com/ajax/libs/pure/0.6.0/grids-responsive-min.css">

    <link rel="stylesheet" href='https://lvs7k.github.io/css/all.min.css'>
    <link href="//maxcdn.bootstrapcdn.com/font-awesome/4.3.0/css/font-awesome.min.css" rel="stylesheet">

    <link rel="alternate" type="application/rss+xml" title="lvs7k&#39;s blog" href='https://lvs7k.github.io/index.xml' />
</head>
<body>


<div id="layout" class="pure-g">
    <div class="sidebar pure-u-1 pure-u-md-1-4">
    <div class="header">
        <hgroup>
            <h1 class="brand-title"><a href="https://lvs7k.github.io/">lvs7k&#39;s blog</a></h1>
            <h2 class="brand-tagline"> Just a Memo </h2>
        </hgroup>

        <nav class="nav">
            <ul class="nav-list">
                
                <li class="nav-item">
                    <a class="pure-button" href="https://twitter.com/lvs7k">
                        <i class="fa fa-twitter"></i> Twitter
                    </a>
                </li>
                
                
                <li class="nav-item">
                    <a class="pure-button" href="https://github.com/lvs7k">
                        <i class="fa fa-github-alt"></i> Github
                    </a>
                </li>
                
                <li class="nav-item">
                    <a class="pure-button" href='https://lvs7k.github.io/index.xml'>
                        <i class="fa fa-rss"></i> rss
                    </a>
                </li>
                <li class="nav-item">
                    <a class="pure-button" href='https://lvs7k.github.io/menu/about'>
                        <i class="fa fa-user"></i> about
                    </a>
                </li>
                <li class="nav-item">
                    <a class="pure-button" href='https://lvs7k.github.io/categories'>
                        <i class="fa fa-folder"></i> categories
                    </a>
                </li>
                <li class="nav-item">
                    <a class="pure-button" href='https://lvs7k.github.io/tags'>
                        <i class="fa fa-tag"></i> tags
                    </a>
                </li>
            </ul>
        </nav>
    </div>
</div>


    <div class="content pure-u-1 pure-u-md-3-4">
        <div>
            
            <div class="posts">
                
                <h1 class="content-subhead">29 Nov 2018, 22:06</h1>
                <section class="post">
                    <header class="post-header">

                        <a href="https://lvs7k.github.io/posts/2018/pccb-easy-7/" class="post-title">Haskellで蟻本（初級編） - GCJの問題に挑戦してみよう（１）</a>

                        <p class="post-meta">
                            
                            
                                categories
                                
                                    <a class="post-category post-category-competitive" href='https://lvs7k.github.io/categories/competitive'>competitive</a>
                                
                            
                        </p>
                        <p class="post-meta">
                            
                                tags
                                
                                    <a class="post-category post-category-haskell" href='https://lvs7k.github.io/tags/haskell'>haskell</a>
                                
                            
                        </p>
                    </header>

                    <div class="post-description">
                        

<p>※蟻本の入力例でしかテストしていません</p>

<p>下2つはコンテストで出たとして解けなそう…。</p>

<h2 id="minimum-scalar-product">Minimum Scalar Product</h2>

<pre><code class="language-haskell">{-# LANGUAGE BangPatterns, FlexibleContexts #-}

import Control.Monad
import Control.Monad.ST
import Data.Array
import Data.Array.ST
import Data.STRef
import Data.List

-- Minimum Scalar Product (2008 Round1A A)
q1 :: Int -&gt; [Int] -&gt; [Int] -&gt; Int
q1 n xs ys = sum $ zipWith (*) xs' ys'
  where
    xs' = sort xs
    ys' = sortBy (flip compare) ys
</code></pre>

<ul>
<li>蟻本の解説参照</li>
</ul>

<hr />

<h2 id="crazy-rows">Crazy Rows</h2>

<pre><code class="language-haskell">-- Crazy Rows (2009 Round2 A)
q2 :: Int -&gt; [String] -&gt; Int
q2 n xss = runST $ do
    a &lt;- newListArray (1, n) (fmap last1 xss) :: ST s (STUArray s Int Int)
    ans &lt;- newSTRef 0
    forM_ [1 .. n] $ \i -&gt; do
        ai &lt;- readArray a i
        when (ai &gt; i) $ do
            js &lt;- flip filterM [i + 1 .. n] $ \j -&gt; do
                aj &lt;- readArray a j
                return $ aj &lt;= i
            when (not $ null js) (swap ans a i (head js))
    readSTRef ans
  where
    last1 xs = length $ dropWhile (/= '1') (reverse xs)
    swap ans a i j
        | i == j    = return ()
        | otherwise = do
            aj  &lt;- readArray a j
            writeArray a j =&lt;&lt; readArray a (j - 1)
            writeArray a (j - 1) aj
            modifySTRef' ans (+ 1)
            swap ans a i (j - 1)
</code></pre>

<ul>
<li><code>MArray</code>面倒くさい</li>
</ul>

<hr />

<h2 id="bribe-the-prisoners">Bribe the Prisoners</h2>

<pre><code class="language-haskell">-- Bribe the Prisoners (2009 Round 1C C)
q3 :: Int -&gt; Int -&gt; [Int] -&gt; Int
q3 p q as = runST $ do
    dp &lt;- newArray ((0, 0), (q + 1, (q + 1))) maxBound :: ST s (STUArray s (Int, Int) Int)
    sequence_ [writeArray dp (i, i + 1) 0 | i &lt;- [0 .. q]]
    forM_ [2 .. q + 1] $ \w -&gt; do
        forM_ [0 .. (q + 1) - w] $ \i -&gt; do
            let j = i + w
            t &lt;- fmap minimum $ forM [i + 1 .. j - 1] $ \k -&gt; do
                dpik &lt;- readArray dp (i, k)
                dpkj &lt;- readArray dp (k, j)
                return (dpik + dpkj)
            writeArray dp (i, j) (t + a ! j - a ! i - 1 - 1)
    readArray dp (0, q + 1)
  where
    a = listArray (0, q + 1) (0 : as ++ [p + 1])
</code></pre>

<ul>
<li><code>dp[i][j] := (i, j)を解放するのに必要な金貨</code>

<ul>
<li><code>例2) P = 20, Q = 3, A = {3, 6, 14}</code>

<ul>
<li>配列Aを作る時に両端を追加して<code>A = [0, 3, 6, 14, 21]</code></li>
<li>例えば<code>dp[0][3]</code>は左端と囚人14は解放済みとして、囚人3と囚人6を解放するのに必要な金貨の最小枚数</li>
</ul></li>
</ul></li>
<li>まず幅が1の時を0で初期化する

<ul>
<li>幅が1、例えば<code>dp[2][3]</code>は間に解放すべき囚人がいないため金貨不要</li>
</ul></li>
<li>幅が2（間に1人解放すべき囚人が存在する）から幅Q+1までループ</li>
<li>最初に解放する囚人をすべて試し、最小コストのものを探す

<ul>
<li>例えば<code>dp[0][3]</code>は<code>dp[0][1] + dp[1][3]</code>と<code>dp[0][2] + dp[2][3]</code>の小さい方</li>
<li>先に囚人3と囚人6のどちらを解放するにしても<code>14 - 0 - 1 - 1</code>の金貨は必要</li>
</ul></li>
</ul>

<hr />

<h2 id="millionaire">Millionaire</h2>

<pre><code class="language-haskell">-- Millionaire (2008 APAC local onsites C)
q4 :: Int -&gt; Double -&gt; Int -&gt; Double
q4 m p x = go m gx
  where
    m2 = 2 ^ m
    go 0 j
        | j == m2   = 1.0
        | otherwise = 0.0
    go i j = maximum $ do
        v &lt;- [0 .. min j (m2 - j)]
        return $ p * (memo ! (i - 1, j + v)) + (1 - p) * (memo ! (i - 1, j - v))
    memo = listArray ((0, 0), (m, m2)) [go i j | i &lt;- [0 .. m], j &lt;- [0 .. m2]]
    gx = (x * m2) `div` 1000000
</code></pre>

<ul>
<li>例として<code>M = 2, P = 0.5, X = 500000</code>を考える</li>
<li><code>dp[i][j] := 残りラウンドがiで、所持金がグループjのとき、最善の戦略をとってお金を持って帰れる確率</code></li>
<li>所持金のグループ

<ul>
<li>ラウンド数が2のとき

<ul>
<li><code>j = 0,         0 ~ 249,999</code></li>
<li><code>j = 1,   250,000 ~ 499,999</code></li>
<li><code>j = 2,   500,000 ~ 749,999</code></li>
<li><code>j = 3,   750,000 ~ 999,999</code></li>
<li><code>j = 4, 1,000,000 ~</code></li>
</ul></li>
</ul></li>

<li><p>配列を埋め終わった状態はこんな感じになる</p>

<pre><code>        [0]    [1]    [2]    [3]    [4]
dp[0] | 0.00 | 0.00 | 0.00 | 0.00 | 1.00 |
dp[1] | 0.00 | 0.00 | 0.50 | 0.50 | 1.00 |
dp[2] | 0.00 | 0.25 | 0.50 | 0.75 | 1.00 |
</code></pre></li>

<li><p>例えば<code>dp[1][2]</code>を更新するときはどうなるか</p>

<ul>
<li>イメージ的には<code>dp[1][2]</code>から見てV字の先の値が必要になって、掛けた金額に応じてV字の角度が変化する（意味不明）</li>
<li>掛けた金額が0なら、勝とうが負けようが<code>dp[0][2]</code>の状態になって</li>
<li>全額かけたなら、勝てば<code>dp[0][4]</code>の状態になって負ければ<code>dp[0][0]</code>の状態になって</li>
<li>その間の金額をかけたら、<code>dp[0][1]</code>, <code>dp[0][3]</code>みたいな…</li>
</ul></li>

<li><p>最初に持っている金額500,000はグループ2なので答えは<code>dp[2][2]</code>の<code>0.50</code></p>

<ul>
<li>蟻本の<code>int i = (ll)X * n / 1000000;</code>の部分で最初の所持金がどのグループか判別している</li>
<li><code>500,000 / (1,000,000 / 2^2) == 500,000 * 2^2 / 1,000,000 == 2</code></li>
</ul></li>
</ul>

                    </div>
                </section>
                
                <h1 class="content-subhead">27 Nov 2018, 22:42</h1>
                <section class="post">
                    <header class="post-header">

                        <a href="https://lvs7k.github.io/posts/2018/pccb-easy-6/" class="post-title">Haskellで蟻本（初級編） - 数学的な問題を解くコツ</a>

                        <p class="post-meta">
                            
                            
                                categories
                                
                                    <a class="post-category post-category-competitive" href='https://lvs7k.github.io/categories/competitive'>competitive</a>
                                
                            
                        </p>
                        <p class="post-meta">
                            
                                tags
                                
                                    <a class="post-category post-category-haskell" href='https://lvs7k.github.io/tags/haskell'>haskell</a>
                                
                            
                        </p>
                    </header>

                    <div class="post-description">
                        

<h2 id="参考資料">参考資料</h2>

<ul>
<li><a href="http://tad311.xsrv.jp/hsmath/">大学数学へのかけ橋！『高校数学＋α ：基礎と論理の物語』</a>

<ul>
<li>文系社会人が高校数学やり直すために何冊か本買ったけどこれが一番好き</li>
<li>PDFが無料でダウンロードできます</li>
</ul></li>
</ul>

<hr />

<p>※蟻本の入力例でしかテストしていません</p>

<h2 id="ユークリッド互除法">ユークリッド互除法</h2>

<pre><code class="language-haskell">{-# LANGUAGE BangPatterns #-}

import qualified Data.IntMap.Strict as IM

-- ユークリッド互除法
gcd' :: Int -&gt; Int -&gt; Int
gcd' x y = go x y
  where
    go a 0 = a
    go a b = go b (a `mod` b)

-- 線分上の格子点の個数
q1 :: (Int, Int) -&gt; (Int, Int) -&gt; Int
q1 (x1, y1) (x2, y2) = gcd' (abs $ x1 - x2) (abs $ y1 - y2) - 1
</code></pre>

<ul>
<li>参考資料の「1.10.2.1 ユークリッドの互除法」を読む</li>
<li>証明も理解する</li>
</ul>

<h3 id="拡張ユークリッド互除法">拡張ユークリッド互除法</h3>

<pre><code class="language-haskell">-- 拡張ユークリッド互除法
extgcd :: Int -&gt; Int -&gt; (Int, Int)
extgcd x y = go x y
  where
    go a 0 = (1, 0)
    go a b = (t, s - q * t)
      where
        (q, r) = a `divMod` b
        (s, t) = extgcd b r

-- 双六
q2 :: Int -&gt; Int -&gt; Maybe (Int, Int, Int, Int)
q2 a b
    | gcd' a b == 1 = case () of
        _ | x &lt; 0, y &lt; 0 -&gt; Just (0, 0, abs x, abs y)
          | x &lt; 0        -&gt; Just (0, y, abs x, 0)
          | y &lt; 0        -&gt; Just (x, 0, 0, abs y)
          | otherwise    -&gt; Just (x, y, 0, 0)
    | otherwise     = Nothing
  where
    (x, y) = extgcd a b
</code></pre>

<ul>
<li><p>拡張ユークリッド互除法で何ができるの？</p>

<ul>
<li><code>ax + by = gcd(a, b)</code>の整数解、<code>(x, y)</code>を求めることができる</li>
</ul></li>

<li><p>上記のプログラムが理解できないんだけど？（蟻本の解説本が欲しいんだけど？）</p>

<pre><code>参考資料の「1.10.3 整数論の基本定理」を読む
整数a，b が互いに素のとき ax + by = 1 は整数解をもつ
整数a，b が互いに素のとき ax + by = c は整数解をもつ
整数a, b が互いに素でなくても c が gcd(a, b) で割り切れるなら ax + by = c は整数解をもつ
ということはax + by = gcd(a, b) は整数解をもつ
a を b で割って a = bq + r として上に式に代入すると
b(qx + y) + rx = gcd(a, b)
s = qx + y, x = t とおいて bs + rt = gcd(s, t) = gcd(a, b)
(b, r) は (a, b) よりも小さいため再帰的に解くことができる
(s, t) がわかっているなら上の式で元の解(x, y) がわかる
停止条件は r == 0 のとき bs + 0t = gcd(b, 0) の解(s, t) = (1, 0)
</code></pre></li>
</ul>

<hr />

<h2 id="素数に関する基本的なアルゴリズム">素数に関する基本的なアルゴリズム</h2>

<pre><code class="language-haskell">-- 素数判定
isPrime :: Int -&gt; Bool
isPrime n = all ((/= 0) . (n `mod`)) $ takeWhile ((&lt;= n) . (^ 2)) (primes n)

-- n以下の素数
primes :: Int -&gt; [Int]
primes n = 2 : 3 : filter isPrime xs
  where
    xs = takeWhile (&lt;= n) [6 * i + j | i &lt;- [1 ..], j &lt;- [-1, 1]]

-- 約数の列挙
divisor :: Int -&gt; [Int]
divisor n = foldr f [] $ takeWhile ((&lt;= n) . (^ 2)) [1 .. n]
  where
    f x ds
        | r == 0, q /= x = x : q : ds
        | r == 0         = x : ds
        | otherwise      = ds
      where
        (q, r) = n `divMod` x

-- 素因数分解
primeFactor :: Int -&gt; IM.IntMap Int
primeFactor n = go IM.empty n $ takeWhile ((&lt;= n) . (^ 2)) (primes n)
  where
    go im 1  _  = im
    go im !k [] = IM.insertWith (+) k 1 im
    go im !k (p:ps)
        | r == 0    = go (IM.insertWith (+) p 1 im) q (p:ps)
        | otherwise = go im k ps
      where
        (q, r) = k `divMod` p
</code></pre>

<h3 id="エラトステネスの篩">エラトステネスの篩</h3>

<pre><code class="language-haskell">-- エラトステネスの篩
sieve :: Int -&gt; [Int]
sieve n = takeWhile (&lt;= n) (sieve' [2 ..])
  where
    sieve' (x:xs) = x : sieve' (filter ((/= 0) . (`mod` x)) xs)

-- 区間内の素数の個数
q5 :: Int -&gt; Int -&gt; Int
q5 a b = go [a .. b - 1] (takeWhile ((&lt; b) . (^ 2)) [2 ..])
  where
    go !ps []     = length ps
    go !ps (x:xs) = go (f ps) (f xs)
      where
        f as = filter ((/= 0) . (`mod` x)) as
</code></pre>

<ul>
<li>Haskellっぽくてかっこいいけど遅い、多分TLE</li>
</ul>

<hr />

<h2 id="べき乗を高速に計算する">べき乗を高速に計算する</h2>

<pre><code class="language-haskell">-- べき乗を高速に計算する
modPow' :: Int -&gt; Int -&gt; Int -&gt; Int
modPow' x n m = go 1 x n
  where
    go !k !_ 0 = k
    go !k !p q
        | r == 1    = go (k * p `mod` m) (p ^ 2 `mod` m) q'
        | otherwise = go k               (p ^ 2 `mod` m) q'
      where
        (q', r) = q `divMod` 2

modPow :: Int -&gt; Int -&gt; Int -&gt; Int
modPow x n m = go n
  where
    go 0 = 1
    go k
        | odd k     = (t * x) `mod` m
        | otherwise = t
      where
        s = go (k `div` 2)
        t = (s * s) `mod` m

-- Carmichael Numbers (UVa No.10006)
q6 :: Int -&gt; Bool
q6 n
    | isPrime n = False
    | otherwise = all f [2 .. n - 1]
  where
    f x = modPow x n n == x `mod` n
</code></pre>

<ul>
<li><code>modPow</code>のほうが分かりやすくて好き</li>
</ul>

                    </div>
                </section>
                
                <h1 class="content-subhead">23 Nov 2018, 17:19</h1>
                <section class="post">
                    <header class="post-header">

                        <a href="https://lvs7k.github.io/posts/2018/pccb-easy-5/" class="post-title">Haskellで蟻本（初級編） - グラフ</a>

                        <p class="post-meta">
                            
                            
                                categories
                                
                                    <a class="post-category post-category-competitive" href='https://lvs7k.github.io/categories/competitive'>competitive</a>
                                
                            
                        </p>
                        <p class="post-meta">
                            
                                tags
                                
                                    <a class="post-category post-category-haskell" href='https://lvs7k.github.io/tags/haskell'>haskell</a>
                                
                            
                        </p>
                    </header>

                    <div class="post-description">
                        

<p>※蟻本の入力例でしかテストしていません</p>

<p><em>TODO: コードをもっと綺麗にする</em></p>

<h2 id="入力の形式について">入力の形式について</h2>

<p>標準入力は下記の形式で与えられるものとする。</p>

<ul>
<li><code>n: 頂点数</code></li>
<li><code>m: 辺の数</code></li>
<li><code>c: 重み</code></li>
</ul>

<pre><code>n m
f1 t1
f2 t2
...
fn tn
</code></pre>

<pre><code>n m
f1 t1 c1
f2 t2 c2
...
fn tn cn
</code></pre>

<h2 id="グラフの表現">グラフの表現</h2>

<p>隣接行列も隣接リストも<code>accumArray</code>を使えば良い。</p>

<h3 id="隣接行列">隣接行列</h3>

<pre><code class="language-haskell">{-# LANGUAGE BangPatterns, FlexibleContexts #-}

import Control.Monad
import Control.Monad.ST
import Data.Array.ST
import Data.Array
import Data.STRef
import Data.List

-- 隣接行列
buildGMatrix :: (Int, Int) -&gt; [(Int, Int)] -&gt; Array (Int, Int) Int
buildGMatrix (i, j) es = accumArray (flip const) 0 ((i, i), (j, j)) (zip es (repeat 1))
</code></pre>

<h3 id="隣接リスト">隣接リスト</h3>

<pre><code class="language-haskell">-- 隣接リスト
buildGList :: (Int, Int) -&gt; [(Int, a)] -&gt; Array Int [a]
buildGList (i, j) es = accumArray (flip (:)) [] (i, j) es
</code></pre>

<ul>
<li><code>containers</code>の<code>Data.Graph.buildG</code>と同じ</li>
</ul>

<hr />

<h2 id="グラフの探索">グラフの探索</h2>

<pre><code class="language-haskell">-- 二部グラフ判定
q1 :: Int -&gt; Int -&gt; [(Int, Int)] -&gt; Bool
q1 n m es = runST $ do
    color &lt;- newArray (0, n - 1) 0 :: ST s (STUArray s Int Int)
    bs &lt;- forM [0 .. n - 1] $ \i -&gt; do
        ci &lt;- readArray color i
        if ci == 0
            then go color 1 i
            else return True
    return (and bs)
  where
    g = buildGList (0, n - 1) es
    go color c i = do
        ci &lt;- readArray color i
        if ci == 0
            then do
                writeArray color i c
                and &lt;$&gt; sequence (fmap (go color (-c)) (g ! i))
            else return (ci == c)
</code></pre>

<hr />

<h2 id="最短経路問題">最短経路問題</h2>

<h3 id="ベルマンフォード法">ベルマンフォード法</h3>

<pre><code class="language-haskell">-- ベルマン-フォード法
bellman :: Int -&gt; Int -&gt; Int -&gt; [(Int, Int, Int)] -&gt; Array Int Int
bellman n m start es = runSTArray $ do
    d &lt;- newArray (0, n - 1) maxBound :: ST s (STArray s Int Int)
    writeArray d start 0
    go d
    return d
 where
    go d = do
        rupd &lt;- newSTRef False
        forM_ es $ \(f, t, c) -&gt; do
            df &lt;- readArray d f
            dt &lt;- readArray d t
            when (df /= maxBound &amp;&amp; df + c &lt; dt) $ do
                writeArray d t (df + c)
                writeSTRef rupd True
        readSTRef rupd &gt;&gt;= \upd -&gt; when upd (go d)
</code></pre>

<ul>
<li>用意するもの

<ul>
<li>辺のリスト</li>
<li>1次元配列

<ul>
<li>初期化：始点のみ<code>0</code>, 残りは<code>INF</code></li>
<li><code>maxBound</code>に加算すると負になってしまうので注意</li>
</ul></li>
</ul></li>
<li>ループ

<ul>
<li>辺のリストに対してループ</li>
<li>辺の始点と終点の距離を配列から取得し、終点の距離を更新</li>
<li>1つでも更新できたら再帰呼び出し</li>
</ul></li>
</ul>

<h3 id="ダイクストラ法">ダイクストラ法</h3>

<pre><code class="language-haskell">-- ダイクストラ法
dijkstra :: Int -&gt; Int -&gt; Int -&gt; [(Int, Int, Int)] -&gt; Array Int Int
dijkstra n m start es = runSTArray $ do
    d &lt;- newArray (0, n - 1) maxBound :: ST s (STArray s Int Int)
    writeArray d start 0
    go d (push (0, start) Empty :: Skew (Int, Int))
    return d
  where
    g = buildGList (0, n - 1) [(a, (b, c)) | (a, b, c) &lt;- es]
    go d que
        | Just ((c, f), q) &lt;- pop que = do
            rq &lt;- newSTRef q
            df &lt;- readArray d f
            when (c &lt;= df) $ do
                forM_ (g ! f) $ \(t, c) -&gt; do
                    dt &lt;- readArray d t
                    when (df + c &lt; dt) $ do
                        writeArray d t (df + c)
                        modifySTRef' rq (\q -&gt; push (df + c, t) q)
            go d =&lt;&lt; readSTRef rq
        | otherwise = return ()
</code></pre>

<ul>
<li>用意するもの

<ul>
<li>グラフ（隣接リスト）</li>
<li>優先度付きキュー

<ul>
<li><code>(コスト, 頂点)</code>の並びでタプルにし、コストの小さい順で取り出せるようにする</li>
<li>初期化：<code>(0, 始点)</code></li>
</ul></li>
<li>1次元配列

<ul>
<li>初期化：始点のみ<code>0</code>, 残りは<code>INF</code></li>
</ul></li>
</ul></li>
<li>ループ

<ul>
<li>キューが空になるまでループ</li>
<li>キューのコストと配列のコストを比較し配列のほうが既に小さいときはスキップ</li>
<li>グラフより隣接する頂点を取り出し、更新できるときは

<ul>
<li>配列を更新</li>
<li>キューにも追加</li>
</ul></li>
</ul></li>
</ul>

<h3 id="ワーシャル-フロイド法">ワーシャル-フロイド法</h3>

<pre><code class="language-haskell">-- ワーシャル-フロイド法
warshall :: Int -&gt; Int -&gt; [(Int, Int, Int)] -&gt; Array (Int, Int) Int
warshall n m es = runSTArray $ do
    d &lt;- newArray ((0, 0), (n - 1, n - 1)) inf
    sequence_ [writeArray d (i, i) 0 | i &lt;- [0 .. n - 1]]
    sequence_ [writeArray d (f, t) c | (f, t, c) &lt;- es]
    forM_ [0 .. n - 1] $ \k -&gt; do
        forM_ [0 .. n - 1] $ \i -&gt; do
            forM_ [0 .. n - 1] $ \j -&gt; do
                dij &lt;- readArray d (i, j)
                dik &lt;- readArray d (i, k)
                dkj &lt;- readArray d (k, j)
                writeArray d (i, j) $ min dij (dik + dkj)
    return d
  where
    inf = 10 ^ 9 + 7
</code></pre>

<ul>
<li>用意するもの

<ul>
<li>辺のリスト</li>
<li>2次元配列

<ul>
<li>初期化

<ul>
<li>始点と終点が同じ場合は<code>0</code></li>
<li>辺が存在する場合はそのコスト</li>
<li>それ以外は<code>INF</code></li>
</ul></li>
</ul></li>
</ul></li>
<li>ループ

<ul>
<li>3重ループ</li>
<li>1番外側のループがその頂点を経由するかしないか</li>
<li>2番目と3番目のループが始点と終点</li>
<li>経由するかしないかで距離が短いほうで更新</li>
</ul></li>
</ul>

<h3 id="経路復元-ダイクストラ法">経路復元（ダイクストラ法）</h3>

<pre><code class="language-haskell">-- 経路復元（ダイクストラ法）
dijkstraPath :: Int -&gt; Int -&gt; Int -&gt; Int -&gt; [(Int, Int, Int)] -&gt; [Int]
dijkstraPath n m start end es = runST $ do
    d &lt;- newArray (0, n - 1) maxBound :: ST s (STArray s Int Int)
    p &lt;- newArray (0, n - 1) (-1) :: ST s (STArray s Int Int)
    writeArray d start 0
    go d p (push (0, start) Empty :: Skew (Int, Int))
    path [end] p
  where
    g = buildGList (0, n - 1) [(a, (b, c)) | (a, b, c) &lt;- es]
    go d p que
        | Just ((c, f), q) &lt;- pop que = do
            rq &lt;- newSTRef q
            df &lt;- readArray d f
            when (c &lt;= df) $ do
                forM_ (g ! f) $ \(t, c) -&gt; do
                    dt &lt;- readArray d t
                    when (df + c &lt; dt) $ do
                        writeArray d t (df + c)
                        writeArray p t f
                        modifySTRef' rq (\q -&gt; push (df + c, t) q)
            go d p =&lt;&lt; readSTRef rq
        | otherwise = return ()
    path (x:xs) p
        | x == start = return (x:xs)
        | otherwise  = do
            pt &lt;- readArray p x
            path (pt:x:xs) p
</code></pre>

<ul>
<li>更新時に手前の頂点をメモっておいて最後に復元する</li>
</ul>

<hr />

<h2 id="最小全域木">最小全域木</h2>

<h3 id="プリム法">プリム法</h3>

<pre><code class="language-haskell">-- プリム法
prim :: Int -&gt; Int -&gt; Int -&gt; [(Int, Int, Int)] -&gt; Int
prim n m start es = runST $ do
    used &lt;- newArray (0, n - 1) False :: ST s (STArray s Int Bool)
    writeArray used start True
    let ss = fmap (\(t, c) -&gt; (c, t)) (g ! start)
    go 0 used (foldr push Empty ss)
  where
    g = buildGList (0, n - 1) [(a, (b, c)) | (a, b, c) &lt;- es]
    go !k used que
        | Just ((c, t), q) &lt;- pop que = do
            usedt &lt;- readArray used t
            if usedt
                then go k used q
                else do
                    let ss = fmap (\(t, c) -&gt; (c, t)) (g ! t)
                    let q' = foldr push q ss
                    writeArray used t True
                    go (k + c) used q'
        | otherwise = return k
</code></pre>

<ul>
<li>用意するもの

<ul>
<li>グラフ（隣接リスト）</li>
<li>優先度付きキュー

<ul>
<li>初期化：始点から伸びている辺を<code>(コスト, 頂点)</code>の並びで追加</li>
</ul></li>
<li>1次元配列

<ul>
<li>既にその頂点が最小全域木に含まれているかをメモする配列</li>
<li>初期化：始点のみ<code>True</code>, 残りは<code>False</code></li>
</ul></li>
</ul></li>
<li>ループ

<ul>
<li>キューが空になるまでループ</li>
<li>キューから取り出した辺の終点が既に最小全域木に含まれている場合はスキップ</li>
<li>そうでなければ採用した辺の終点から伸びている辺を取り出しキューに追加</li>
<li>採用した辺の終点をメモし、再帰</li>
</ul></li>
</ul>

<h3 id="クラスカル法">クラスカル法</h3>

<pre><code class="language-haskell">-- クラスカル法
kruskal :: Int -&gt; Int -&gt; [(Int, Int, Int)] -&gt; Int
kruskal n m es = runST $ do
    uf &lt;- newUF (0, n - 1)
    rc &lt;- newSTRef 0
    forM_ es' $ \(f, t, c) -&gt; do
        isSame &lt;- same uf f t
        when (not isSame) $ do
            unite uf f t
            modifySTRef' rc (+ c)
    readSTRef rc
  where
    es' = sortBy (\(_, _, a) (_, _, b) -&gt; a `compare` b) es
</code></pre>

<ul>
<li>用意するもの

<ul>
<li>辺のリスト

<ul>
<li>コストの小さい順にソート</li>
</ul></li>
<li>Union-Find木</li>
</ul></li>
<li>ループ

<ul>
<li>コストの小さい辺から見ていく</li>
<li>辺の始点と終点が既に同じグループならスキップ</li>
<li>そうでなければその辺を採用して始点と終点を同じグループにする</li>
</ul></li>
</ul>

<hr />

<h2 id="練習問題">練習問題</h2>

<p>初級とか言ってるけど難しすぎでは…？</p>

<pre><code class="language-haskell">-- Roadblocks (POJ No.3255)
q2 :: Int -&gt; Int -&gt; [(Int, Int, Int)] -&gt; Int
q2 n m es = runST $ do
    d1 &lt;- newListArray (1, 4) (0 : repeat inf) :: ST s (STUArray s Int Int)
    d2 &lt;- newListArray (1, 4) (0 : repeat inf) :: ST s (STUArray s Int Int)
    go d1 d2 (push (0, 1) Empty)
    readArray d2 n
  where
    inf = 10 ^ 9 + 7
    g = buildGList (1, n) [(f, (t, c)) | (f, t, c) &lt;- es]
    go d1 d2 que
        | Just ((cost, v), que') &lt;- pop que = do
            rque &lt;- newSTRef que'
            d2v &lt;- readArray d2 v
            when (cost &lt;= d2v) $ do
                forM_ (g ! v) $ \(t, c) -&gt; do
                    d1t &lt;- readArray d1 t
                    d2t &lt;- readArray d2 t
                    let d2c = cost + c
                    when (d2c &lt; d1t) $ do
                        writeArray d1 t d2c
                        modifySTRef' rque $ \q -&gt; push (d2c, t) q
                    when (d2c &lt; d2t &amp;&amp; d1t &lt; d2c) $ do
                        writeArray d2 t d2c
                        modifySTRef' rque $ \q -&gt; push (d2c, t) q
            go d1 d2 =&lt;&lt; readSTRef rque
        | otherwise = return ()
</code></pre>

<ul>
<li>ダイクストラ法で最短距離を求めつつ2番目の最短路も求める</li>
<li>キューから取り出して最短距離は更新できないが2番目の最短路が更新できるなら更新</li>
</ul>

<pre><code class="language-haskell">-- Conscription (POJ No.3723)
q3 :: Int -&gt; Int -&gt; Int -&gt; [(Int, Int, Int)] -&gt; Int
q3 n m r es = runST $ do
    uf &lt;- newUF (0, n + m - 1)
    k &lt;- kruskal 0 uf es'
    return $ 10000 * (n + m) + k
  where
    es' = sortBy (\(_, _, c1) (_, _, c2) -&gt; c1 `compare` c2)
        $ concat [[(f, n + t, -c), (n + t, f, -c)] | (f, t, c) &lt;- es]
    kruskal !k uf []             = return k
    kruskal !k uf ((f, t, c):rs) = do
        isSame &lt;- same uf f t
        if isSame
            then kruskal k uf rs
            else do
                unite uf f t
                kruskal (k + c) uf rs
</code></pre>

<ul>
<li>辺のコストを負にして最小全域木を求める</li>
<li>なぜこれで答えが求まるのかいまいちわかってない</li>
</ul>

<pre><code class="language-haskell">-- Layout (POJ No.3169)
q4 :: Int -&gt; Int -&gt; Int -&gt; [(Int, Int, Int)] -&gt; [(Int, Int, Int)] -&gt; Int
q4 n ml md as bs = runST $ do
    d &lt;- newListArray (1, n) (0 : repeat maxBound) :: ST s (STUArray s Int Int)
    forM_ [1 .. n] $ \_ -&gt; do
        forM_ as  $ \e -&gt; update d e
        forM_ bs' $ \e -&gt; update d e
        forM_ cs  $ \e -&gt; update d e
    d1 &lt;- readArray d 1
    dn &lt;- readArray d n
    case () of
        _ | d1 &lt; 0         -&gt; return (-1)
          | dn == maxBound -&gt; return (-2)
          | otherwise      -&gt; return dn
  where
    bs' = [(t, f, -c) | (f, t, c) &lt;- bs]
    cs  = [(i + 1, i, 0) | i &lt;- [1 .. n - 1]]
    update d (f, t, c) = do
        df &lt;- readArray d f
        when (df &lt; maxBound) $ do
            dt &lt;- readArray d t
            writeArray d t (min dt (df + c))
</code></pre>

<ul>
<li>普通の最短路問題

<ul>
<li><code>d(v) + w &gt;= d(u)</code>

<ul>
<li>頂点vから頂点uへコストwの辺が存在</li>
</ul></li>
</ul></li>
<li>この問題

<ul>
<li><code>d[i+1] + 0     &gt;= d[i]</code>

<ul>
<li>頂点i+1から頂点iへコスト0の辺が存在</li>
</ul></li>
<li><code>d[AL]  + DL    &gt;= d[BL]</code>

<ul>
<li>頂点ALから頂点BLへコストDLの辺が存在</li>
</ul></li>
<li><code>d[BD]  + (-DD) &gt;= d[AD]</code>

<ul>
<li>頂点BDから頂点ADへコスト-DDの辺が存在</li>
</ul></li>
</ul></li>
<li>負の辺が含まれるためベルマン-フォード法で最短経路を求める</li>
<li>負の閉路が存在する場合

<ul>
<li>例）1と3は仲良しで距離10以内、2と3は仲悪くて距離11以上離す</li>
</ul></li>
<li>いくらでも離れることが可能な場合

<ul>
<li>例）Nは誰とも仲良くない</li>
</ul></li>
</ul>

                    </div>
                </section>
                
                <h1 class="content-subhead">20 Nov 2018, 21:01</h1>
                <section class="post">
                    <header class="post-header">

                        <a href="https://lvs7k.github.io/posts/2018/pccb-easy-4/" class="post-title">Haskellで蟻本（初級編） - データ構造</a>

                        <p class="post-meta">
                            
                            
                                categories
                                
                                    <a class="post-category post-category-competitive" href='https://lvs7k.github.io/categories/competitive'>competitive</a>
                                
                            
                        </p>
                        <p class="post-meta">
                            
                                tags
                                
                                    <a class="post-category post-category-haskell" href='https://lvs7k.github.io/tags/haskell'>haskell</a>
                                
                            
                        </p>
                    </header>

                    <div class="post-description">
                        

<p>※蟻本の入力例でしかテストしていません</p>

<h2 id="優先度付きキュー">優先度付きキュー</h2>

<blockquote>
<p>多くのプログラミング言語では効率的に実装されたプライオリティキューが標準で含まれています。</p>
</blockquote>

<p>と書かれているが、Haskellには標準で含まれていない。</p>

<p>じゃあどうするかというと</p>

<ol>
<li><code>containers</code>の<code>Map</code>か<code>Set</code>の<code>Min/Max</code>の関数で頑張る</li>
<li>自分で作る</li>
</ol>

<p>の2択になると思う。</p>

<hr />

<h2 id="leftist-heap">Leftist Heap</h2>

<pre><code class="language-haskell">module Heap
    ( Heap (..)
    , meld
    , push
    , pop
    ) where

import Control.Monad

data Heap a
    = Empty
    | Node !Int !a !(Heap a) !(Heap a)
    deriving Show

rank :: Heap a -&gt; Int
rank Empty          = 0
rank (Node s _ _ _) = s

heap :: a -&gt; Heap a -&gt; Heap a -&gt; Heap a
heap x a b
    | ra &lt; rb   = Node (ra + 1) x b a
    | otherwise = Node (rb + 1) x a b
  where
    ra = rank a
    rb = rank b

meld :: Ord a =&gt; Heap a -&gt; Heap a -&gt; Heap a
meld a     Empty = a
meld Empty b     = b
meld a@(Node _ ax al ar) b@(Node _ bx bl br)
    | ax &lt; bx   = heap ax al (meld ar b)
    | otherwise = heap bx bl (meld br a)

push :: Ord a =&gt; a -&gt; Heap a -&gt; Heap a
push x a = meld a (Node 1 x Empty Empty)

pop :: Ord a =&gt; Heap a -&gt; Maybe (a, Heap a)
pop Empty          = Nothing
pop (Node _ x l r) = Just (x, meld l r)

test :: (Show a, Ord a) =&gt; [a] -&gt; IO ()
test xs = do
    heap &lt;- flip (flip foldM Empty) xs $ \h i -&gt; do
        let h' = push i h
        print h'
        return h'
    putStrLn &quot;--------------------&quot;
    flip (flip foldM_ heap) [1 .. length xs] $ \h _ -&gt; do
        print h
        let Just (_, h') = pop h
        return h'
</code></pre>

<pre><code>*Heap&gt; test [4,1,2,5,3]
Node 1 4 Empty Empty
Node 1 1 (Node 1 4 Empty Empty) Empty
Node 2 1 (Node 1 4 Empty Empty) (Node 1 2 Empty Empty)
Node 2 1 (Node 1 4 Empty Empty) (Node 1 2 (Node 1 5 Empty Empty) Empty)
Node 2 1 (Node 2 2 (Node 1 5 Empty Empty) (Node 1 3 Empty Empty)) (Node 1 4 Empty Empty)
--------------------
Node 2 1 (Node 2 2 (Node 1 5 Empty Empty) (Node 1 3 Empty Empty)) (Node 1 4 Empty Empty)
Node 2 2 (Node 1 5 Empty Empty) (Node 1 3 (Node 1 4 Empty Empty) Empty)
Node 2 3 (Node 1 4 Empty Empty) (Node 1 5 Empty Empty)
Node 1 4 (Node 1 5 Empty Empty) Empty
Node 1 5 Empty Empty
</code></pre>

<hr />

<h2 id="skew-heap">Skew Heap</h2>

<ul>
<li>Leftist Heapから木のバランスを取る処理を除いたもの</li>
<li>実装が軽いのに速さもそんな変わらないというのをどこかで見た</li>
<li>大きい順に取り出したいときは要修正</li>
</ul>

<pre><code class="language-haskell">module Skew
    ( Skew (..)
    , meld
    , push
    , pop
    ) where

import Control.Monad

data Skew a
    = Empty
    | Node !a !(Skew a) !(Skew a)
    deriving Show

meld :: Ord a =&gt; Skew a -&gt; Skew a -&gt; Skew a
meld a     Empty = a
meld Empty b     = b
meld a@(Node ax al ar) b@(Node bx bl br)
    | ax &lt; bx   = Node ax al (meld ar b)
    | otherwise = Node bx bl (meld br a)

push :: Ord a =&gt; a -&gt; Skew a -&gt; Skew a
push x a = meld a (Node x Empty Empty)

pop :: Ord a =&gt; Skew a -&gt; Maybe (a, Skew a)
pop Empty        = Nothing
pop (Node x l r) = Just (x, meld l r)

test :: (Show a, Ord a) =&gt; [a] -&gt; IO ()
test xs = do
    skew &lt;- flip (flip foldM Empty) xs $ \h i -&gt; do
        let h' = push i h
        print h'
        return h'
    putStrLn &quot;--------------------&quot;
    flip (flip foldM_ skew) [1 .. length xs] $ \h _ -&gt; do
        print h
        let Just (_, h') = pop h
        return h'
</code></pre>

<pre><code>*Skew&gt; test [4,1,2,5,3]
Node 4 Empty Empty
Node 1 Empty (Node 4 Empty Empty)
Node 1 Empty (Node 2 Empty (Node 4 Empty Empty))
Node 1 Empty (Node 2 Empty (Node 4 Empty (Node 5 Empty Empty)))
Node 1 Empty (Node 2 Empty (Node 3 Empty (Node 4 Empty (Node 5 Empty Empty))))
--------------------
Node 1 Empty (Node 2 Empty (Node 3 Empty (Node 4 Empty (Node 5 Empty Empty))))
Node 2 Empty (Node 3 Empty (Node 4 Empty (Node 5 Empty Empty)))
Node 3 Empty (Node 4 Empty (Node 5 Empty Empty))
Node 4 Empty (Node 5 Empty Empty)
Node 5 Empty Empty
</code></pre>

<hr />

<h2 id="プライオリティキューを用いる問題">プライオリティキューを用いる問題</h2>

<pre><code class="language-haskell">-- Expedition (POJ 2431)
q1 :: Int -&gt; Int -&gt; Int -&gt; [Int] -&gt; [Int] -&gt; Int
q1 n l p as bs = go 0 p Empty (zip bs as)
  where
    go !k fuel pque bas
        | fuel &gt;= l    = k
        | Just ((g, _), pque'') &lt;- pop pque'
            = go (k + 1) (fuel + g) pque'' ys
        | otherwise = -1
      where
        (xs, ys) = span ((&lt;= fuel) . snd) bas
        pque' = foldr push pque xs
</code></pre>

<ul>
<li>fuelがゴールまで足りていたら補給回数を返す</li>
<li>そうでなければ通り過ぎたガソリンスタンドを全てキューに追加し、補給できる量が最大のものを取り出す</li>
<li>取り出せた場合は再帰、取り出せなかったら-1</li>
</ul>

<pre><code class="language-haskell">-- Fence Repair (POJ 3253)
q2 :: Int -&gt; [Int] -&gt; Int
q2 n ls = go 0 (foldr push Empty ls)
  where
    go !k s
        | Just (x1, s1) &lt;- pop s
        , Just (x2, s2) &lt;- pop s1
            = let x = x1 + x2 in go (k + x) (push x s2)
        | otherwise = k
</code></pre>

<hr />

<h2 id="union-find木">Union-Find木</h2>

<ul>
<li>経路圧縮のみ</li>
</ul>

<pre><code class="language-haskell">module UF
    ( UF (..)
    , newUF
    , root
    , same
    , unite
    ) where

import Control.Monad
import Control.Monad.ST
import Data.Array.ST

newtype UF s
    = UF (STUArray s Int Int)

newUF :: (Int, Int) -&gt; ST s (UF s)
newUF (i, j) = UF &lt;$&gt; (newListArray (i, j) [i .. j])

root :: UF s -&gt; Int -&gt; ST s Int
root (UF a) i = do
    p &lt;- readArray a i
    if p == i
        then return p
        else do
            rp &lt;- root (UF a) p
            writeArray a i rp
            return rp

same :: UF s -&gt; Int -&gt; Int -&gt; ST s Bool
same uf x y = (==) &lt;$&gt; (root uf x) &lt;*&gt; (root uf y)

unite :: UF s -&gt; Int -&gt; Int -&gt; ST s ()
unite uf@(UF a) i j = do
    ri &lt;- root uf i
    rj &lt;- root uf j
    when (ri /= rj) $ do
        writeArray a rj ri
</code></pre>

<hr />

<h2 id="union-find木を用いる問題">Union-Find木を用いる問題</h2>

<pre><code class="language-haskell">-- 食物連鎖 (POJ 1182)
q3 :: Int -&gt; Int -&gt; [(Int, Int, Int)] -&gt; Int
q3 n k qs = runST $ do
    uf &lt;- newUF (1, n * 3)
    go 0 uf qs
  where
    go !f _  [] = return f
    go !f uf ((1, x, y):rs) = do
        let p1 = or [x &lt; 1, y &lt; 1, x &gt; n, y &gt; n]
        p2 &lt;- (||) &lt;$&gt; same uf x (y + n) &lt;*&gt; same uf x (y + n * 2)
        if p1 || p2
            then go (f + 1) uf rs
            else do
                unite uf x y
                unite uf (x + n) (y + n)
                unite uf (x + n * 2) (y + n * 2)
                go f uf rs
    go !f uf ((2, x, y):rs) = do
        let p1 = or [x &lt; 1, y &lt; 1, x &gt; n, y &gt; n]
        p2 &lt;- (||) &lt;$&gt; same uf x y &lt;*&gt; same uf x (y + n * 2)
        if p1 || p2
            then go (f + 1) uf rs
            else do
                unite uf x (y + n)
                unite uf (x + n) (y + n * 2)
                unite uf (x + n * 2) y
                go f uf rs
</code></pre>

<ul>
<li><p><code>n = 100</code>のときUnion-Findの要素数はその3倍として</p>

<ul>
<li><code>1: 1が種類A</code>, <code>101: 1が種類B</code>, <code>201: 1が種類C</code></li>
<li><code>2: 2が種類A</code>, <code>102: 2が種類B</code>, <code>202: 2が種類C</code></li>
<li><code>...</code></li>
</ul></li>

<li><p>与えられた情報より、同時に起こるものを同じグループとしていく</p>

<ul>
<li><code>情報1: 1と2は同じ種類です。</code>

<ul>
<li><code>unite uf 1 2</code>, <code>unite uf 101 102</code>, <code>unite uf 201 202</code></li>
</ul></li>
<li><code>情報2: 1と2を食べます。</code>

<ul>
<li><code>unite uf 1 102</code>, <code>unite uf 101 202</code>, <code>unite uf 201 2</code></li>
</ul></li>
</ul></li>
</ul>

                    </div>
                </section>
                
                <h1 class="content-subhead">19 Nov 2018, 22:09</h1>
                <section class="post">
                    <header class="post-header">

                        <a href="https://lvs7k.github.io/posts/2018/pccb-easy-3/" class="post-title">Haskellで蟻本（初級編） - 動的計画法</a>

                        <p class="post-meta">
                            
                            
                                categories
                                
                                    <a class="post-category post-category-competitive" href='https://lvs7k.github.io/categories/competitive'>competitive</a>
                                
                            
                        </p>
                        <p class="post-meta">
                            
                                tags
                                
                                    <a class="post-category post-category-haskell" href='https://lvs7k.github.io/tags/haskell'>haskell</a>
                                
                            
                        </p>
                    </header>

                    <div class="post-description">
                        <p>※蟻本の入力例でしかテストしていません</p>

<pre><code class="language-haskell">{-# LANGUAGE BangPatterns, FlexibleContexts #-}

import Control.Monad
import Data.List
import qualified Data.ByteString.Char8 as B

import Control.Monad.ST
import Data.Array.ST
import Data.Array
import qualified Data.Map.Strict as M

-- 01 ナップサック問題
q1 :: Int -&gt; Int -&gt; [(Int, Int)] -&gt; Int
q1 n m wvs = go n m
  where
    wva = listArray (1, n) wvs
    memo = listArray ((0, 0), (n, m)) [go i j | i &lt;- [0 .. n], j &lt;- [0 .. m]]
    go 0 _ = 0
    go i j
        | j &lt; w     = x1
        | otherwise = max x1 x2
      where
        (w, v) = wva ! i
        x1 = memo ! (i - 1, j)
        x2 = memo ! (i - 1, j - w) + v

q1' :: Int -&gt; Int -&gt; [(Int, Int)] -&gt; Int
q1' n m wvs = runST $ do
    dp &lt;- newArray ((0, 0), (n, m)) 0 :: ST s (STUArray s (Int, Int) Int)
    forM_ [1 .. n] $ \i -&gt; do
        let (w, v) = wva ! i
        forM_ [0 .. m] $ \j -&gt; do
            x1 &lt;- readArray dp (i - 1, j)
            if j &lt; w
                then writeArray dp (i, j) x1
                else do
                    x2 &lt;- readArray dp (i - 1, j - w)
                    writeArray dp (i, j) (max x1 (x2 + v))
    readArray dp (n, m)
  where
    wva = listArray (1, n) wvs

q1'' :: Int -&gt; Int -&gt; [(Int, Int)] -&gt; Int
q1'' n m wvs = runST $ do
    dp   &lt;- newArray ((0, 0), (n, m)) 0     :: ST s (STUArray s (Int, Int) Int)
    memo &lt;- newArray ((0, 0), (n, m)) False :: ST s (STUArray s (Int, Int) Bool)
    sequence_ [writeArray memo (0, i) True | i &lt;- [0 .. m]]
    go dp memo (n, m)
  where
    wva = listArray (1, n) wvs
    go dp memo (i, j) = do
        exists &lt;- readArray memo (i, j)
        when (not exists) $ do
            let (w, v) = wva ! i
            x1 &lt;- go dp memo (i - 1, j)
            if j &lt; w
                then do
                    writeArray dp (i, j) x1
                else do
                    x2 &lt;- go dp memo (i - 1, j - w)
                    writeArray dp (i, j) (max x1 (x2 + v))
            writeArray memo (i, j) True
        readArray dp (i, j)
</code></pre>

<ul>
<li>メモ化再帰　これでAC取りたい</li>
<li>動的計画法　圧倒的速さ</li>
<li>メモ化再帰　上のでTLEになるがメモ化再帰したいとき？</li>
</ul>

<pre><code class="language-haskell">-- 最長共通部分列問題
q2 :: Int -&gt; Int -&gt; B.ByteString -&gt; B.ByteString -&gt; Int
q2 n m ss ts = go (n, m)
  where
    memo = listArray ((0, 0), (n, m)) [go (i, j) | i &lt;- [0 .. n], j &lt;- [0 .. m]]
    go (0, _) = 0
    go (_, 0) = 0
    go (i, j)
        | si == tj  = maximum [x1, x2, x3]
        | otherwise = max x1 x2
      where
        si = ss `B.index` (i - 1)
        tj = ts `B.index` (j - 1)
        x1 = memo ! (i - 1, j)
        x2 = memo ! (i, j - 1)
        x3 = memo ! (i - 1, j - 1) + 1
</code></pre>

<ul>
<li><code>ByteString</code>の<code>index</code>は<code>O(1)</code></li>
<li><code>where</code>句に細かく名前つけてあげると読みやすくなる気がする</li>
</ul>

<pre><code class="language-haskell">-- 個数制限なしナップサック問題
q3 :: Int -&gt; Int -&gt; [(Int, Int)] -&gt; Int
q3 n m wvs = go (n, m)
  where
    wva  = listArray (1, n) wvs
    memo = listArray ((0, 0), (n, m)) [go (i, j) | i &lt;- [0 .. n], j &lt;- [0 .. m]]
    go (0, _) = 0
    go (i, j)
        | j &lt; w     = x1
        | otherwise = max x1 x2
      where
        (w, v) = wva ! i
        x1 = memo ! (i - 1, j)
        x2 = memo ! (i, j - w) + v
</code></pre>

<ul>
<li>このように計算量を減らすのだって書いてあるけど思いつけるようになるのだろうか</li>
</ul>

<pre><code class="language-haskell">-- 01 ナップサック問題 その2
q4 :: Int -&gt; Int -&gt; [(Int, Int)] -&gt; Int
q4 n m wvs = maximum $ do
    v &lt;- [0 .. vmax]
    let w = memo ! (n, v)
    guard (w &lt;= m)
    return v
  where
    inf = 10 ^ 10
    vmax = sum $ fmap snd wvs
    wva = listArray (1, n) wvs
    memo = listArray ((0, 0), (n, vmax)) [go (i, j) | i &lt;- [0 .. n], j &lt;- [0 .. vmax]]
    go (0, 0) = 0
    go (0, _) = inf
    go (i, j)
        | j &lt; v     = x1
        | otherwise = min x1 x2
      where
        (w, v) = wva ! i
        x1 = go (i - 1, j)
        x2 = go (i - 1, j - v) + w
</code></pre>

<pre><code class="language-haskell">-- 個数制限つき部分和問題
q5 :: Int -&gt; Int -&gt; [Int] -&gt; [Int] -&gt; Bool
q5 n k as ms
    | go (n, k) &lt; 0 = False
    | otherwise     = True
  where
    aa = listArray (1, n) as
    ma = listArray (1, n) ms
    memo = listArray ((0, 0), (n, k)) [go (i, j) | i &lt;- [0 .. n], j &lt;- [0 .. k]]
    go (0, 0) = 0
    go (0, _) = -1
    go (i, j)
        | x1 &gt;= 0          = m
        | j &lt; a || x2 &lt;= 0 = -1
        | otherwise        = x2 - 1
      where
        a = aa ! i
        m = ma ! i
        x1 = memo ! (i - 1, j)
        x2 = memo ! (i, j - a)
</code></pre>

<pre><code class="language-haskell">-- 最長増加部分列問題
q6 :: Int -&gt; [Int] -&gt; Int
q6 n as = go n
  where
    aa = listArray (1, n) as
    memo = listArray (1, n) [go i | i &lt;- [1 .. n]]
    go 1 = 1
    go i = maximum . (1:) $ do
        j &lt;- [1 .. i - 1]
        let (ai, aj) = (aa ! i, aa ! j)
        guard (aj &lt; ai)
        return $ (memo ! j) + 1

q6' :: Int -&gt; [Int] -&gt; Int
q6' n as = maximum $ foldl' f M.empty as
  where
    f m a
        | Just (k, v) &lt;- M.lookupGE a m = M.insert a v $ M.delete k m
        | otherwise                     = M.insert a (M.size m + 1) m
</code></pre>

<ul>
<li><code>IntMap</code>と異なり<code>Map</code>の<code>size</code>は<code>O(1)</code></li>
<li>蟻本の「DPテーブルの変化」の図と同じように<code>Map</code>が更新されていく</li>
</ul>

<pre><code class="language-haskell">-- 分割数
q7 :: Int -&gt; Int -&gt; Int -&gt; Int
q7 n m d = go (m, n)
  where
    memo = listArray ((0, 0), (m, n)) [go (i, j) | i &lt;- [0 .. m], j &lt;- [0 .. n]]
    go (0, 0) = 1
    go (0, _) = 0
    go (i, j)
        | j &lt; i     = x1
        | otherwise = (x1 + x2) `mod` d
      where
        x1 = memo ! (i - 1, j)
        x2 = memo ! (i, j - 1)
</code></pre>

<pre><code>漸化式がよく理解できなかったのでメモ

dp[3][4] = 4 (4の3分割の総数)

  (4, 0, 0)
  (3, 1, 0) -- dp[2][4]と同じ
  (2, 2, 0)

  (2, 1, 1) -- 4の3分割 (全て1以上) == (4 - 3)の3分割 (全て0以上)
</code></pre>

<pre><code class="language-haskell">-- 重複組合せ
q8 :: Int -&gt; Int -&gt; Int -&gt; [Int] -&gt; Int
q8 n m d as = go (n, m)
  where
    aa = listArray (1, n) as
    memo = listArray ((0, 0), (n, m)) [go (i, j) | i &lt;- [0 .. n], j &lt;- [0 .. m]]
    go (0, 0) = 1
    go (0, _) = 0
    go (i, j)
        | j - 1 &lt; 0      = x1
        | j - 1 - ai &lt; 0 = (x1 + x2) `mod` d
        | otherwise      = (x1 + x2 - x3) `mod` d
      where
        ai = aa ! i
        x1 = memo ! (i - 1, j)
        x2 = memo ! (i, j - 1)
        x3 = memo ! (i - 1, j - 1 - ai)
</code></pre>

<ul>
<li><a href="http://d.hatena.ne.jp/phyllo_algo/20140831/1409502655">重複組み合わせ - れどこだ目指すよ！　(；`・ω・）</a>

<ul>
<li>上の図と下の図の矢印が繋がっている□を見比べる</li>
<li><code>X = C + B</code>だと<code>A</code>が余計に足されているので引いてあげる</li>
</ul></li>
</ul>

                    </div>
                </section>
                
                <h1 class="content-subhead">18 Nov 2018, 12:39</h1>
                <section class="post">
                    <header class="post-header">

                        <a href="https://lvs7k.github.io/posts/2018/pccb-easy-2/" class="post-title">Haskellで蟻本（初級編） - 貪欲法</a>

                        <p class="post-meta">
                            
                            
                                categories
                                
                                    <a class="post-category post-category-competitive" href='https://lvs7k.github.io/categories/competitive'>competitive</a>
                                
                            
                        </p>
                        <p class="post-meta">
                            
                                tags
                                
                                    <a class="post-category post-category-haskell" href='https://lvs7k.github.io/tags/haskell'>haskell</a>
                                
                            
                        </p>
                    </header>

                    <div class="post-description">
                        <p>※蟻本の入力例でしかテストしていません</p>

<pre><code class="language-haskell">{-# LANGUAGE BangPatterns, FlexibleContexts #-}

import Control.Monad
import Data.List

import Control.Monad.ST
import Data.Array.ST
import Data.STRef
import Data.Array         (Array)
import Data.Array.Unboxed (UArray)
import Data.Array.IArray
import qualified Data.Set as S

-- 硬貨の問題
q1 :: Int -&gt; Int -&gt; Int -&gt; Int -&gt; Int -&gt; Int -&gt; Int -&gt; Int
q1 n c1 c5 c10 c50 c100 c500 = go 0 n cs
  where
    cs = zip [500, 100, 50, 10, 5, 1] [c500, c100, c50, c10, c5, c1]
    go !k a [] = k
    go !k a ((s, t):ds)
        | a &gt;= s &amp;&amp; t &gt; 0 = go (k + 1) (a - s) ((s, t - 1):ds)
        | otherwise       = go k a ds
</code></pre>

<pre><code class="language-haskell">-- 区間スケジューリング問題
q2 :: Int -&gt; [(Int, Int)] -&gt; Int
q2 n ss = go 0 (sortOn snd ss)
  where
    go !k [] = k
    go !k ((s, t):ts) = go (k + 1) (filter ((&gt;= t) . fst) ts)
</code></pre>

<pre><code class="language-haskell">-- Best Cow Line (POJ 3617)
q3 :: Int -&gt; String -&gt; String
q3 n ss = reverse $ go &quot;&quot; ss
  where
    go !ts &quot;&quot; = ts
    go !ts us
        | us &lt;= us' = go (head us  : ts) (tail us)
        | otherwise = go (head us' : ts) (tail us')
      where
        us' = reverse us
</code></pre>

<pre><code class="language-haskell">-- Saruman's Army (POJ 3069)
q4 :: Int -&gt; Int -&gt; [Int] -&gt; Int
q4 n r xs = go 0 (sort xs)
  where
    go !k []     = k
    go !k (y:ys) = go (k + 1) cs
      where
        (as, bs) = span (&lt;= y + r) (y:ys)
        cs = dropWhile (&lt;= last as + r) bs
</code></pre>

<pre><code class="language-haskell">-- Fence Repair (POJ 3253)
q5 :: Int -&gt; [Int] -&gt; Int
q5 n ls = go 0 n (S.fromList $ zip ls [1 ..])
  where
    go !k m s
        | Just ((v1, _), s')  &lt;- S.minView s
        , Just ((v2, _), s'') &lt;- S.minView s'
            = go (k + v1 + v2) (m + 1) (S.insert (v1 + v2, m + 1) s'')
        | otherwise = k
</code></pre>

<ul>
<li><code>Data.Set</code>を優先度付きキューとして使用している</li>
<li>値が重複しないように<code>(&lt;値&gt;, &lt;識別番号&gt;)</code>を入れている</li>
</ul>

                    </div>
                </section>
                
                <h1 class="content-subhead">17 Nov 2018, 20:40</h1>
                <section class="post">
                    <header class="post-header">

                        <a href="https://lvs7k.github.io/posts/2018/pccb-easy-1/" class="post-title">Haskellで蟻本（初級編） - 全探索</a>

                        <p class="post-meta">
                            
                            
                                categories
                                
                                    <a class="post-category post-category-competitive" href='https://lvs7k.github.io/categories/competitive'>competitive</a>
                                
                            
                        </p>
                        <p class="post-meta">
                            
                                tags
                                
                                    <a class="post-category post-category-haskell" href='https://lvs7k.github.io/tags/haskell'>haskell</a>
                                
                            
                        </p>
                    </header>

                    <div class="post-description">
                        <p>※蟻本の入力例でしかテストしていません</p>

<pre><code class="language-haskell">{-# LANGUAGE BangPatterns, FlexibleContexts #-}

import Control.Monad
import Control.Monad.ST
import Data.Array.ST
import Data.Array.Unboxed (UArray)
import Data.Array.IArray
import Data.STRef
import qualified Data.Set as S

-- 部分和問題
q1 :: Int -&gt; Int -&gt; [Int] -&gt; Bool
q1 n k as = go 0 as
  where
    go !i []
        | i == k    = True
        | otherwise = False
    go !i (b:bs) = go (i + b) bs || go i bs

q1' :: Int -&gt; Int -&gt; [Int] -&gt; Bool
q1' n k as = go k as
  where
    go !i []
        | i == 0    = True
        | otherwise = False
    go !i (b:bs) = go (i - b) bs || go i bs

q1'' :: Int -&gt; Int -&gt; [Int] -&gt; Bool
q1'' n k as = any (== k) $ do
    bs &lt;- sequence (replicate n [True, False])
    let cs = [a | (a, True) &lt;- zip as bs]
    return (sum cs)
</code></pre>

<ul>
<li><code>q1''</code>はビットを使った状態の列挙</li>
</ul>

<pre><code class="language-haskell">-- Lake Counting (POJ No.2386)
q2 :: Int -&gt; Int -&gt; [String] -&gt; Int
q2 n m xss = runST $ do
    as &lt;- newListArray ((1, 1), (n, m)) (concat xss) :: ST s (STUArray s (Int, Int) Char)
    refc &lt;- newSTRef 0
    forM_ [1 .. n] $ \i -&gt; do
        forM_ [1 .. m] $ \j -&gt; do
            c &lt;- readArray as (i, j)
            when (c == 'W') $ do
                dfs as (i, j)
                modifySTRef' refc (+ 1)
    readSTRef refc
  where
    dfs as (i, j) = do
        c &lt;- readArray as (i, j)
        case c of
            '.' -&gt; return ()
            _   -&gt; do
                writeArray as (i, j) '.'
                mapM_ (dfs as) next
      where
        next = do
            s &lt;- [i - 1, i, i + 1]
            t &lt;- [j - 1, j, j + 1]
            guard (inRange ((1, 1), (n, m)) (s, t))
            return (s, t)
</code></pre>

<pre><code class="language-haskell">-- 迷路の最短路
q3 :: Int -&gt; Int -&gt; [String] -&gt; Int
q3 n m xss = fst . head $ dropWhile ((/= goal) . snd) queue
  where
    (start, _) = head . filter ((== 'S') . snd)
               $ zip [(i, j) | i &lt;- [1 .. n], j &lt;- [1 .. m]] (concat xss)
    (goal, _)  = head . filter ((== 'G') . snd)
               $ zip [(i, j) | i &lt;- [1 .. n], j &lt;- [1 .. m]] (concat xss)
    maze = listArray ((1, 1), (n, m)) (concat xss) :: UArray (Int, Int) Char
    queue = (0, start) : go (S.singleton start) queue
    go s ((dist, (i, j)):qs) = next' ++ go s' qs
      where
        next  = filter f2 $ filter f1 [(i - 1, j), (i + 1, j), (i, j - 1), (i, j + 1)]
        next' = [(dist + 1, p) | p &lt;- next]
        s' = foldr S.insert s next
        f1 pos
            | inRange ((1, 1), (n, m)) pos
            , (maze ! pos) `elem` ['.', 'G'] = True
            | otherwise                      = False
        f2 = (`S.notMember` s)
</code></pre>

<ul>
<li>コード汚いけど…</li>
<li>余再帰を使って無限リスト<code>queue</code>を作っている

<ul>
<li>余再帰よく理解できてない</li>
<li><a href="https://kazu-yamamoto.hatenablog.jp/entry/20121107/1352259739">なぜ Haskell ではキューが軽んじられているか？</a></li>
</ul></li>
<li>既にその場所を訪れたかの判定に<code>Data.Set</code>を渡している</li>
<li>最後に無限リスト<code>queue</code>からゴール取り出して終了</li>
</ul>

<p>キューが空になったら…っていうのがやりたいときは</p>

<ul>
<li><code>go</code>の引数でキューの中身の数を管理して<code>0</code>になったら終了

<ul>
<li><code>[]</code>でパターンマッチはできないことに注意</li>
</ul></li>
<li>素直に<code>Data.Sequence</code>を使う</li>
<li>自分で<code>data Queue a = Queue [a] [a]</code>みたいにキューを作る

<ul>
<li>左側から取り出して右側に追加する</li>
<li>左側から取り出す時に空だったら右側を<code>reverse</code>して左側にセットする</li>
</ul></li>
</ul>

                    </div>
                </section>
                
                <h1 class="content-subhead">12 Nov 2018, 21:47</h1>
                <section class="post">
                    <header class="post-header">

                        <a href="https://lvs7k.github.io/posts/2018/dynamic-programming-in-haskell/" class="post-title">Haskellで動的計画法（準備編）</a>

                        <p class="post-meta">
                            
                            
                                categories
                                
                                    <a class="post-category post-category-competitive" href='https://lvs7k.github.io/categories/competitive'>competitive</a>
                                
                            
                        </p>
                        <p class="post-meta">
                            
                                tags
                                
                                    <a class="post-category post-category-haskell" href='https://lvs7k.github.io/tags/haskell'>haskell</a>
                                
                            
                        </p>
                    </header>

                    <div class="post-description">
                        

<h2 id="知りたいこと">知りたいこと</h2>

<ul>
<li>Haskellで動的計画法を書く時にどういう書き方をすればいいのか</li>
</ul>

<h2 id="試してみたこと">試してみたこと</h2>

<ul>
<li><code>f(n) = f(n - 1) + 1</code></li>
<li>初項1、公差1の等差数列の和を<code>n = 10 ^ 6</code>と<code>n = 10 ^ 7</code>で計算</li>
<li>PowerShellの<code>Measure-Command</code>で時間を計測</li>
<li>時間計測時に<code>stack run</code>や<code>stack exec</code>を使うと200ms~400ms余計に時間がかかった</li>
</ul>

<h2 id="結論">結論</h2>

<ul>
<li><code>f1</code>でTLEにならないなら<code>f1</code></li>
<li>ボトムアップに配列埋める場合は<code>f2</code></li>
<li><code>f1</code>では間に合わないがメモ化再帰したい場合は<code>f3</code></li>
<li>間違ったこと言ってたらごめんなさい</li>
</ul>

<pre><code class="language-haskell">{-# LANGUAGE BangPatterns #-}

import Control.Monad
import Control.Monad.ST
import Data.List
import Data.Array
import Data.Array.ST
import qualified Data.IntMap.Strict as IMS
import Control.Monad.Trans.Class
import Control.Monad.Trans.Cont

-- f(n) = f(n - 1) + 1
-- stack build
-- Measure-Command { ./stack-work/~.exe | Out-Default } | Select-Object TotalMilliseconds

-- n = 10 ^ 6 :  477 ms
-- n = 10 ^ 7 : 4308 ms
f1 :: Int -&gt; Int
f1 n = go n
  where
    memo = listArray (1, n) (fmap go [1 .. n])
    go 1 = 1
    go k = memo ! (k - 1) + 1
</code></pre>

<ul>
<li>メモ化再帰 (<code>Array</code>)</li>
<li>これ以下の速度のものは使う必要がない</li>
</ul>

<pre><code class="language-haskell">-- n = 10 ^ 6 :  14 ms
-- n = 10 ^ 7 :  97 ms
f2 :: Int -&gt; Int
f2 n = runST $ do
    a &lt;- newArray (1, n) 1 :: ST s (STUArray s Int Int)
    forM_ [2 .. n] $ \i -&gt; do
        b &lt;- readArray a (i - 1)
        writeArray a i (b + 1)
    readArray a n
</code></pre>

<ul>
<li>動的計画法 (<code>UArray</code>)</li>
<li>最速</li>
</ul>

<pre><code class="language-haskell">-- n = 10 ^ 6 :  159 ms
-- n = 10 ^ 7 : 1850 ms
f3 :: Int -&gt; Int
f3 n = runST $ do
    a &lt;- newArray (1, n) 1     :: ST s (STUArray s Int Int)
    m &lt;- newArray (1, n) False :: ST s (STUArray s Int Bool)
    writeArray m 1 True
    go a m n
  where
    go :: STUArray s Int Int -&gt; STUArray s Int Bool -&gt; Int -&gt; ST s Int
    go a m i = do
        evaluated &lt;- readArray m i
        if evaluated
            then readArray a i
            else do
                b &lt;- go a m (i - 1)
                writeArray a i (b + 1)
                writeArray m i True
                return (b + 1)
</code></pre>

<ul>
<li>メモ化再帰 (<code>UArray</code>)</li>
<li>関数呼び出しのオーバーヘッドなのか大分遅くなる</li>
</ul>

<pre><code class="language-haskell">-- n = 10 ^ 6 :  192 ms
-- n = 10 ^ 7 : 1654 ms
f4 :: Int -&gt; Int
f4 n = runST $ do
    a &lt;- newArray (1, n) 1     :: ST s (STUArray s Int Int)
    m &lt;- newArray (1, n) False :: ST s (STUArray s Int Bool)
    writeArray m 1 True
    go a m n
  where
    go :: STUArray s Int Int -&gt; STUArray s Int Bool -&gt; Int -&gt; ST s Int
    go a m i = evalContT . callCC $ \cc -&gt; do
        evaluated &lt;- lift $ readArray m i
        when evaluated $ do
            b &lt;- lift $ readArray a i
            cc b
        c &lt;- lift $ go a m (i - 1)
        lift $ writeArray a i (c + 1)
        lift $ writeArray m i True
        return (c + 1)
</code></pre>

<ul>
<li>速さとは関係ないがインデントが深くなるのが嫌なら<code>Cont</code>モナドを使う</li>
</ul>

<pre><code class="language-haskell">-- n = 10 ^ 6 :  404 ms
-- n = 10 ^ 7 : 4369 ms
f5 :: Int -&gt; Int
f5 n = runST $ do
    a &lt;- newArray (1, n) Nothing :: ST s (STArray s Int (Maybe Int))
    writeArray a 1 (Just 1)
    go a n
    Just i &lt;- readArray a n
    return i
  where
    go :: STArray s Int (Maybe Int) -&gt; Int -&gt; ST s Int
    go a i = do
        mi &lt;- readArray a i
        case mi of
            Just b  -&gt; return b
            Nothing -&gt; do
                c &lt;- go a (i - 1)
                writeArray a i (Just (c + 1))
                return (c + 1)
</code></pre>

<ul>
<li>メモ化再帰 (<code>Array</code>)</li>
<li>計算済みかどうかを<code>Array</code>に<code>Maybe Int</code>を入れて判断した場合</li>
</ul>

<pre><code class="language-haskell">-- n = 10 ^ 6 :  459 ms
-- n = 10 ^ 7 : 3615 ms
f6 :: Int -&gt; Int
f6 n = (foldl' f im [2 .. n]) IMS.! n
  where
    im = IMS.singleton 1 1 :: IMS.IntMap Int
    f m i = let b = m IMS.! (i - 1)
            in IMS.insert i (b + 1) m
</code></pre>

<ul>
<li>動的計画法 (<code>IntMap.Strict</code>)</li>
<li><code>Lazy</code>はこれより大分遅かったので省略</li>
</ul>

<pre><code class="language-haskell">-- n = 10 ^ 6 :  516 ms
-- n = 10 ^ 7 : 4380 ms
f7 :: Int -&gt; Int
f7 n = (go im n) IMS.! n
  where
    im = IMS.singleton 1 1 :: IMS.IntMap Int
    go !m i
        | Just _ &lt;- IMS.lookup i m = m
        | otherwise                = IMS.insert i (b + 1) m'
      where
        m' = go m (i - 1)
        b  = m' IMS.! (i - 1)
</code></pre>

<ul>
<li>メモ化再帰 (<code>IntMap.Strict</code>)</li>
<li><code>Lazy</code>はこれより大分遅かったので省略</li>
</ul>

                    </div>
                </section>
                
            </div>
            

            <div class="footer">
    <div class="pure-menu pure-menu-horizontal pure-menu-open">
        <ul>
            <li>Powered by <a class="hugo" href="https://gohugo.io/" target="_blank">hugo</a></li>
        </ul>
    </div>
</div>
<script src='https://lvs7k.github.io/js/all.min.js'></script>

        </div>
    </div>
</div>

<script>
(function(i,s,o,g,r,a,m){i['GoogleAnalyticsObject']=r;i[r]=i[r]||function(){
(i[r].q=i[r].q||[]).push(arguments)},i[r].l=1*new Date();a=s.createElement(o),
m=s.getElementsByTagName(o)[0];a.async=1;a.src=g;m.parentNode.insertBefore(a,m)
})(window,document,'script','//www.google-analytics.com/analytics.js','ga');

ga('create', 'UA-129286122-1', 'auto');
ga('send', 'pageview');

</script>

</body>
</html>
